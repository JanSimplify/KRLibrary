# SPDX-License-Identifier: MIT-0
# SPDX-FileCopyrightText: Copyright (c) 2025 JanSimplify

@PACKAGE_INIT@

cmake_minimum_required(VERSION 3.25 FATAL_ERROR)

option(
    @PROJECT_NAME@_USE_SHARED_LIBS
    "Default to SHARED libraries"
    "${BUILD_SHARED_LIBS}"
)

block(PROPAGATE
    ${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
    ${CMAKE_FIND_PACKAGE_NAME}_FOUND
)
    set(shared_export "@PACKAGE_config_install_dir@/@shared_export@.cmake")
    set(static_export "@PACKAGE_config_install_dir@/@static_export@.cmake")

    set(installed_components)
    set(installed_exports)

    if(EXISTS ${shared_export})
        list(APPEND installed_components "shared")
        list(APPEND installed_exports ${shared_export})
    endif()

    if(EXISTS ${static_export})
        list(APPEND installed_components "static")
        list(APPEND installed_exports ${static_export})
    endif()

    foreach(comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
        if((NOT comp IN_LIST installed_components)
        AND ${CMAKE_FIND_PACKAGE_NAME}_FIND_REQUIRED_${comp})
            set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "@PROJECT_NAME@ missing required component: ${comp}"
            )
            set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
            return()
        endif()
    endforeach()

    foreach(exp IN LISTS installed_exports)
        include(${exp})
    endforeach()

    if(@PROJECT_NAME@_USE_SHARED_LIBS)
        add_library(@library_alias@ ALIAS @shared_alias@)
    else()
        add_library(@library_alias@ ALIAS @static_alias@)
    endif()
endblock()
